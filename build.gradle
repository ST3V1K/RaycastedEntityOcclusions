import org.bxteam.runserver.ServerType

buildscript {
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-stdlib:1.9.24"
    }
}

plugins {
    id 'java'
    id("org.bxteam.runserver") version "1.2.1"
}

group = 'games.cubi'
version = '1.6.2'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = uri("https://repo.codemc.io/repository/maven-releases/") }

    maven { url = uri("https://repo.codemc.io/repository/maven-snapshots/") }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT")
    compileOnly("com.github.retrooper:packetevents-spigot:2.8.0")
}

tasks {
    runServer {
        serverType(ServerType.PURPUR)
        serverVersion("1.21.4")
        noGui(true)
        acceptMojangEula()

        downloadPlugins {
            it.modrinth("packetevents", "2.9.4+spigot")
            it.github("dmulloy2", "ProtocolLib", "5.4.0", "ProtocolLib.jar")
        }
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

jar {
    manifest {
        attributes "paperweight-mappings-namespace": "mojang"
    }
}